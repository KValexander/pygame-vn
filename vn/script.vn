name n1 = ("Первый", WHITE)
name n2 = ("Второй", WHITE)

character char = ("character.png", (0.2; 0.2))

count i = 0
count j = 0

start:

	background "class.jpg"

	"Первая реплика для демонстрации безименованных реплик"

	"Задний фон появился благодаря команде background "class.jpg", где "class.jpg" - это ссылка на изображение "

	"Команда background получает изображения из папки assets/images/medley/"

	"Большая реплика. Текст для наполнения реплики различным текстовым содержанием для демонстрации перехода текста на следующую строку"

	"После этой реплики на экране появится персонаж"

	show char

	"Персонаж появился благодаря команде show char, где char - это заранее заданная переменная до метки start в скрипте, в формате character n = ("src.png", (0; 0)). character - тип переменной, n - название переменной, "src.png" - ссылка до изображения, (0.0; 0.0) - координаты появления, от 0.0 до 1.0 "

	"Переменная персонажа получает изображение из папки assets/images/characters/"

	"Изменение путей пока не продекларировано"

	"Дальше будут идти именованные реплики"

	n1 "— Говорит первый персонаж"

	n2 "— Отвечает второй"

	n1 "— Вновь говорит первый"

	"Именованные реплики начинаются с переменной, содержащей имя персонажа, после чего уже идёт сама реплика. Формат var "реплика" "

	"Переменные имён задаются до метки start, в формате name n = ("Name", (255,255,255)), где name - это тип переменной, , n - название переменной, "Name" - отображающееся имя, (255,255,255) - цвет имени"

	"Дальше вы переходите на новую метку с помощью команду go! new, где go! - сама команда, а new - название метки"

	condition "Желаете перейти на новую метку?":
		"Да": i++; go! new
		"Нет": j++; continue

	"В таком случае останемся здесь"

	"Но нам всё же придётся перейти к следующей метке для продолжения объяснений команд приложения"

	go! transitional

label new:

	background "teachers.jpg"

	"Вы находитесь на новой метке. Метки в скрипте обозначаются командой label new:, где label - объявление метки, а new - название. Наличие двоеточия является обязательным"

	"Задний фон всё так же был изменён командой background "src.jpg" "

	"При следующем клике персонаж исчезнет с экрана"

	hide char

	"Персонаж исчез благодаря команде hide char, где char - это переменная персонажа"

	"Продолжение диалога"

	n1 "— Первый говорит."

	n2 "— Второй вторит."

	n1 "— Первый продолжает."

	n2 "— Второй начинает."

	"Конец диалога. Вновь переход на безименованные реплики"

	"Переходим к следующей метке"

	go! transitional

label transitional:

	"После этой реплики произойдёт сравнение переменных типа счётчик, объявленых до начала скрипта."

	if j == 1: go! notswitched
	elif i == 1: go! switched
	else: continue

	"Программа ещё недоделана, и в случае реализации операторов if elif else вы сюда попасть не должны"

	"На данный момент реализован функционал только самого оператора if. elif и else ещё не реализованы"

	go! common

label notswitched:

	"Вы отказались переходить на другую метку."

	go! common

label switched:

	"Вы согласились перейти на другую метку."

	go! common

label common:

	"Только что вы увидели комментарий на своё действие совершённое парой реплик назад."

	"Для определения вашего выбора были использованы переменные с типом данных счётчик, в которые записались значения выбора"

	"Переменные счётчика могут изменяться только внутри операторов condition и if"

	"Оператор if сравнивает числа и значения счётчиков. Операторы сравнения: ==, >=, <=, >, <, !="

	"В случае если сравнение не проходит, то строка идёт дальше. Если за ней следует команда elif, то исполняется код этого оператора, в ином случае код исполняет команды оператора else если он есть, или дальше идёт по линии скрипта"

	"Хотелось бы ещё реализовать изменение цвета текста, доработать основную логику, логику перехода на предыдущие линии, добавить возможность вывода переменных в тексте, команд вывода и затухания звука и команды анимации вывода заднего фона и персонажей."

	"Конец короткой демонстрации. Следующее нажатие закроет окно"

	return